000000**************************************************************/   28311915
000001*  PROGRAM: PPFEDTAX                                         */   28311915
000002*  RELEASE: ___1915______ SERVICE REQUEST(S): ____82831____  */   28311915
000003*  NAME:_BASKAR CHITRAVEL MODIFICATION DATE:  ___03/30/10__  */   28311915
000004*  DESCRIPTION:                                              */   28311915
000005*  CALCULATE AND ADD THE ADDITIONAL TAX FOR NRAS USING INPUT */   28311915
000006*  NRA RATE SET LOADED INTO CPWSXFTA'S FEDTAX TABLE ARRAY.   */   28311915
000007**************************************************************/   28311915
000008**************************************************************/   15611721
000009*  PROGRAM: PPFEDTAX                                         */   15611721
000010*  RELEASE: ___1721______ SERVICE REQUEST(S): ____81561____  */   15611721
000011*  NAME:_____RGLASER_____ MODIFICATION DATE:  ___07/05/06__  */   15611721
000012*  DESCRIPTION:                                              */   15611721
000013*  ADD LOGIC TO INCREASE NRA EMPLOYEE'S GROSS AMT BEFORE THE */   15611721
000014*  COMPUTATION OF FEDERAL WITHHOLDING TAX.  THE AMOUNT THAT  */   15611721
000015*  IS ADDED COMES FROM 1 OF 3 NEW SYSTEM PARMS AND DEPENDS   */   15611721
000016*  ON THE EMPLOYEE'S PAYROLL SCHEDULE.  THIS CHANGE IS       */   15611721
000017*  REQUIRED IN ORDER TO REMAIN IN COMPLAINCE WITH IRS        */   15611721
000018*  REGULATIONS IN RESPECT TO THE AMOUNT WITHHELD FOR NRA     */   15611721
000019*  EMPLOYEES. THE INCREASED GROSS AMT IS NOT CARRIED FURTHER.*/   15611721
000020*  THE INCREASED GROSS AMT IS NOT CARRIED FOR FURTHER        */   15611721
000021*  PROCESSING.                                               */   15611721
000022**************************************************************/   15611721
000101**************************************************************/   69761691
000102*  PROGRAM: PPFEDTAX                                         */   69761691
000103*  RELEASE: ___1691______ SERVICE REQUEST(S): __16976,16963  */   69761691
000104*  NAME:________JLT______ MODIFICATION DATE:  ___01/12/06__  */   69761691
000105*  DESCRIPTION:                                              */   69761691
000106*  -TREAT CITIZEN CODE "A" IN THE SAME MANNER AS 'N' IN MOST */   69761691
000107*   CASES.                                                   */   69761691
000108*  -REMOVE THE BACKING OUT OF FEL GROSS FROM PRIMARY FWT GRS.*/   69761691
000109*   (I.E., THE 'FEL' DOS FWT SUBJECT GRS IND IS NOW SET OFF).*/   69761691
000111*  - GENERATE A MESSAGE IF THE FWT EXCEEDS $99,999.99.       */   69761691
000112**************************************************************/   69761691
000120**************************************************************/   69331612
000200*  PROGRAM: PPFEDTAX                                         */   69331612
000300*  RELEASE: ___1612______ SERVICE REQUEST(S): ___16933_____  */   69331612
000400*  NAME:________JLT______ MODIFICATION DATE:  ___10/20/04__  */   69331612
000500*  DESCRIPTION:                                              */   69331612
000600*  - ADD PROCESSING FOR FELLOWSHIP GROSS (POSTDOC).          */   69331612
000610**************************************************************/   69331612
000700**************************************************************/   48861503
000701*  PROGRAM:  PPFEDTAX                                        */   48861503
000702*  RELEASE: ___1503______ SERVICE REQUEST(S): ____14886____  */   48861503
000703*  NAME:___S.ISAACS______ MODIFICATION DATE:  ___07/23/03__  */   48861503
000704*   DESCRIPTION:                                             */   48861503
000706*     ADDED CAPABILITY TO HANDLE FED FLAT TAX ON BONUSES     */   48861503
000707*      IF REQUIRED.                                          */   48861503
000710**************************************************************/   36100633
000800*  PROGRAM:  PPFEDTAX                                        */   36100633
000900*  RELEASE # ___0633___   SERVICE REQUEST NO(S)____3610______*/   36100633
001000*  NAME __SRS__________   MODIFICATION DATE ____07/24/91_____*/   36100633
001100*  DESCRIPTION                                               */   36100633
001200*                                                            */   36100633
001300*  THIS IS A NEW PROGRAM FOR ORCA                            */   36100633
001400*                                                            */   36100633
001500**************************************************************/   36100633
001600 IDENTIFICATION DIVISION.                                         PPFEDTAX
001700 PROGRAM-ID. PPFEDTAX.                                            PPFEDTAX
001800 INSTALLATION. UNIVERSITY OF CALIFORNIA.                          PPFEDTAX
001900 AUTHOR.                                                          PPFEDTAX
002000 DATE-WRITTEN.                                                    PPFEDTAX
002100 DATE-COMPILED.                                                   PPFEDTAX
002200*REMARKS.                                                         PPFEDTAX
002300******************************************************************PPFEDTAX
002400*                                                                *PPFEDTAX
002500*  THIS MODULE COMPUTES FEDERAL TAXES.                           *PPFEDTAX
003000*                                                                *PPFEDTAX
003100******************************************************************PPFEDTAX
003200 ENVIRONMENT DIVISION.                                            PPFEDTAX
003300     SKIP3                                                        PPFEDTAX
003400******************************************************************PPFEDTAX
003500*                                                                *PPFEDTAX
003600*                         DATA DIVISION                          *PPFEDTAX
003700*                                                                *PPFEDTAX
003800******************************************************************PPFEDTAX
003900 DATA DIVISION.                                                   PPFEDTAX
004000     SKIP1                                                        PPFEDTAX
004200******************************************************************PPFEDTAX
004300*                                                                *PPFEDTAX
004400*                    WORKING-STORAGE SECTION                     *PPFEDTAX
004500*                                                                *PPFEDTAX
004600******************************************************************PPFEDTAX
004700 WORKING-STORAGE SECTION.                                         PPFEDTAX
004800     SKIP1                                                        PPFEDTAX
006900 77  S4                           PIC S9(4)  COMP.                PPFEDTAX
006901 77  S5                           PIC S9(4)  COMP.                PPFEDTAX
006902 77  S6                           PIC S9(4)  COMP.                PPFEDTAX
006910 77  M40020                       PIC 9(05)  VALUE 40020.         PPFEDTAX
006912 77  M40090                       PIC 9(05)  VALUE 40090.         69761691
006920 77  WK-TOT-FWT-ADJ-GRS           PIC S9(7)V99  COMP-3.           69331612
006940 77  WS-DED-TO-TAKE               PIC S9(7)V99  COMP-3.           69761691
013300     EJECT                                                        PPFEDTAX
013400******************************************************************PPFEDTAX
013500*                                                                *PPFEDTAX
013600*                    LINKAGE SECTION                             *PPFEDTAX
013700*                                                                *PPFEDTAX
013800******************************************************************PPFEDTAX
013900 LINKAGE SECTION.                                                 PPFEDTAX
016099 01  FEDERAL-TAX-AREA.                                            PPFEDTAX
016100                                  COPY CPWSXFTA.                  PPFEDTAX
016183     EJECT                                                        PPFEDTAX
016336 01  FEDERAL-TAX-INTERFACE.                                       PPFEDTAX
016337                                  COPY CPLNKFED.                  PPFEDTAX
019290     EJECT                                                        PPFEDTAX
019291******************************************************************PPFEDTAX
019292*                                                                *PPFEDTAX
019293*                       PROCEDURE DIVISION                       *PPFEDTAX
019294*                                                                *PPFEDTAX
019295******************************************************************PPFEDTAX
019296     SKIP2                                                        PPFEDTAX
019300 PROCEDURE DIVISION       USING FEDERAL-TAX-AREA                  PPFEDTAX
019310                                FEDERAL-TAX-INTERFACE.            PPFEDTAX
019400     SKIP2                                                        PPFEDTAX
019500 0100-FEDERAL-TAX-ROUTINE  SECTION.                               69331612
019600 0100-FEDERAL-TAX-DEDUCTION.                                      PPFEDTAX
019700     SKIP1                                                        PPFEDTAX
019800     MOVE  ZERO    TO KFED-FEL-FWT-DED-TO-TAKE.                   69331612
019801     IF      KFED-POSTDOC-FEL-NRA-GRS  > ZERO                     69331612
019802             PERFORM  1000-POSTDOC-FEL-NRA-DED                    69331612
019803     END-IF.                                                      69331612
019804     SKIP1                                                        15611721
019810     IF      KFED-CT-CODE-NON-RESID-ALIEN-T                       15611721
019811     OR      KFED-CT-CODE-NONR-ALN-SPECCTRY                       15611721
019813             PERFORM  1500-NRA-DED-PROCESSING                     15611721
019814             THRU     1500-EXIT                                   15611721
019816     END-IF.                                                      15611721
019820     IF      KFED-GROSS-FWT1  < ZERO                              PPFEDTAX
019900             COMPUTE KFED-GROSS-FWT2 =                            PPFEDTAX
020000                     KFED-GROSS-FWT2 + KFED-GROSS-FWT1            PPFEDTAX
020100             MOVE ZEROS             TO KFED-GROSS-FWT1            PPFEDTAX
020110     END-IF.                                                      PPFEDTAX
020200     SKIP1                                                        PPFEDTAX
020300     IF      KFED-GROSS-FWT2  < ZERO                              PPFEDTAX
020400             COMPUTE KFED-GROSS-FWT1 =                            PPFEDTAX
020500                     KFED-GROSS-FWT1 + KFED-GROSS-FWT2            PPFEDTAX
020600             MOVE ZEROS             TO KFED-GROSS-FWT2            PPFEDTAX
020610     END-IF.                                                      PPFEDTAX
020620     SKIP1                                                        48861503
020630     IF      KFED-GROSS-FWT3  < ZERO                              48861503
020640             COMPUTE KFED-GROSS-FWT1 =                            48861503
020650                     KFED-GROSS-FWT1 + KFED-GROSS-FWT3            48861503
020660             MOVE ZEROS             TO KFED-GROSS-FWT3            48861503
020670     END-IF.                                                      48861503
020700     SKIP1                                                        PPFEDTAX
020800     IF      KFED-GROSS-FWT1 = ZEROS                              PPFEDTAX
020900             MOVE KFED-REDUCTIONS-FWT TO KFED-AMOUNT              PPFEDTAX
021000         IF  KFED-REDUCTIONS-FWT < ZERO                           PPFEDTAX
021100             NEXT SENTENCE                                        PPFEDTAX
021200         ELSE                                                     PPFEDTAX
021300             MOVE ZEROS TO KFED-DED-TO-TAKE                       PPFEDTAX
021400             GO TO 0500-FEDERAL-TAX-GROSS                         PPFEDTAX
021401         END-IF                                                   PPFEDTAX
021410     END-IF.                                                      PPFEDTAX
021500     SKIP1                                                        PPFEDTAX
021600     PERFORM                                                      PPFEDTAX
021700        VARYING S4 FROM 1 BY 1                                    PPFEDTAX
021800          UNTIL XFTA-PERIOD (S4) = KFED-TAX-PERIOD                PPFEDTAX
021900             OR S4 > 4                                            PPFEDTAX
022000     END-PERFORM.                                                 PPFEDTAX
022100     SKIP2                                                        PPFEDTAX
022200     IF      S4 > +4                                              PPFEDTAX
022300             MOVE +1 TO S4                                        PPFEDTAX
022500             MOVE M40020 TO KFED-MSSG-NO                          PPFEDTAX
022700     END-IF.                                                      PPFEDTAX
022800     SKIP1                                                        PPFEDTAX
023400******************************************************************PPFEDTAX
023500*    IF  TAX PERIOD INPUT BY USER IS NOT MATCHED - DEFAULT TO THE PPFEDTAX
023600*    FIRST TAX TABLE LOADED FOR THIS CYCLE - ISSUE MESSAGE TO     PPFEDTAX
023700*    USER - AND CONTINUE TO PROCESSING - S4 NOW POINTS            PPFEDTAX
023800*    TO THE PROPER TAX TABLE - S5 WILL LATER POINT TO THE ITEM    PPFEDTAX
023900*    IN THE TAX TABLE - MARRIED STARTS AT POSITION 12 - SINGLE    PPFEDTAX
024000*    POSITION 1. S6 WILL POINT TO END OF TABLE (S=11, M=22).      PPFEDTAX
024100******************************************************************PPFEDTAX
024110     SKIP1                                                        PPFEDTAX
024200     IF      KFED-GROSS-FWT1 > ZEROS                              PPFEDTAX
024300         OR  KFED-AMOUNT  <  ZEROES                               PPFEDTAX
024400             COMPUTE KFED-BASE = KFED-GROSS-FWT1 -                PPFEDTAX
024500                                        KFED-REDUCTIONS-FWT       PPFEDTAX
024600             IF      KFED-BASE < ZEROS                            PPFEDTAX
024700                     COMPUTE KFED-AMOUNT = - KFED-BASE            PPFEDTAX
024800                     MOVE ZEROS TO KFED-BASE                      PPFEDTAX
024900             ELSE                                                 PPFEDTAX
024901                     MOVE ZEROS TO KFED-AMOUNT                    PPFEDTAX
024910             END-IF                                               PPFEDTAX
025000     ELSE                                                         PPFEDTAX
025010             MOVE KFED-REDUCTIONS-FWT TO KFED-AMOUNT              PPFEDTAX
025100             MOVE ZEROS TO KFED-BASE                              PPFEDTAX
025110     END-IF.                                                      PPFEDTAX
025120     SKIP1                                                        PPFEDTAX
025200     IF      KFED-FEDTAX-MARITAL = 'M'                            PPFEDTAX
025300     AND NOT KFED-CT-CODE-NON-RESID-ALIEN-T                       PPFEDTAX
025320     AND NOT KFED-CT-CODE-NONR-ALN-SPECCTRY                       69761691
025400             MOVE 12 TO S5                                        PPFEDTAX
025500             COMPUTE S6 = XFTA-NO-MARRIED (S4) + 12               PPFEDTAX
025600     ELSE                                                         PPFEDTAX
025610             MOVE  1 TO S5                                        PPFEDTAX
025700             COMPUTE S6 = XFTA-NO-SINGLES (S4) + 1                PPFEDTAX
025710     END-IF.                                                      PPFEDTAX
025800     SKIP1                                                        PPFEDTAX
025880******************************************************************15611721
025890* THE STATEMENT BELOW TREATS NON-RESIDENTS (CITIZENSHIP CODE OF N)15611721
025891* AS IF THEY CLAIM ONLY 1 FED WITHHOLDING EXEMPTION AND IGNORES   15611721
025893* WHAT THE EMPLOYEE CLAIMED FOR WITHHOLDING.                      15611721
025894* WHEREAS FOR ALL OTHER CITIZENSHIP CODES INCLUDING CITIZENSHIP   15611721
025895* CODES OF 'A', FED WITHHOLDING EXEMPTION CLAIMED BY THE EMPLOYEE 15611721
025896* IS USED IN THE COMPUTATION OF THE BASE SALARY AMOUNT.           15611721
025897******************************************************************15611721
025900     IF      KFED-CT-CODE-NON-RESID-ALIEN-T                       PPFEDTAX
026000             COMPUTE KFED-BASE = KFED-BASE - XFTA-EXEMPTION (S4)  PPFEDTAX
026200     ELSE                                                         PPFEDTAX
026300             COMPUTE KFED-BASE = KFED-BASE                        PPFEDTAX
026400                    - (XFTA-EXEMPTION (S4) * KFED-FEDTAX-EXEMPT)  PPFEDTAX
026410     END-IF.                                                      PPFEDTAX
026420     SKIP1                                                        PPFEDTAX
026500     IF      KFED-BASE < ZEROS                                    PPFEDTAX
026600             MOVE ZEROS TO KFED-BASE                              PPFEDTAX
026700     END-IF.                                                      PPFEDTAX
026800     IF      KFED-FDTX-EXEMPT-EXMPT-FED-TX                        PPFEDTAX
026900       OR   (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                     PPFEDTAX
026910             NOT KFED-CT-CODE-NONR-ALN-SPECCTRY AND               69761691
027000             NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                  PPFEDTAX
027100             MOVE ZEROS TO KFED-DED-TO-TAKE                       PPFEDTAX
027200     ELSE                                                         PPFEDTAX
027210             IF   KFED-BASE NOT > XFTA-SALARY (S4, S5)            PPFEDTAX
027300                  MOVE ZEROS TO KFED-DED-TO-TAKE                  PPFEDTAX
027301                                WS-DED-TO-TAKE                    28311915
027310                  PERFORM 2000-CALC-NRA-DEDUCTION THRU 2000-EXIT  28311915
027320                  MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE         28311915
027400             ELSE                                                 PPFEDTAX
027402                  PERFORM                                         PPFEDTAX
027410                    VARYING S5 FROM S5 BY +1                      PPFEDTAX
027420                      UNTIL S5 = S6                               PPFEDTAX
027430                         OR XFTA-SALARY (S4, S5) > KFED-BASE      PPFEDTAX
027440                  END-PERFORM                                     PPFEDTAX
027700                  ADD -1 TO S5                                    PPFEDTAX
027810*****             COMPUTE KFED-DED-TO-TAKE ROUNDED =              69761691
027820                  COMPUTE WS-DED-TO-TAKE  ROUNDED =               69761691
027900                  XFTA-TAX (S4, S5) + ((KFED-BASE -               PPFEDTAX
028000                  XFTA-SALARY (S4, S5)) * XFTA-PCT (S4, S5))      PPFEDTAX
028004                  PERFORM 2000-CALC-NRA-DEDUCTION THRU 2000-EXIT  28311915
028022                  MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE         69761691
028023                  IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE        69761691
028024                      MOVE M40090 TO KFED-MSSG-NO                 69761691
028025                  END-IF                                          69761691
028026             END-IF                                               PPFEDTAX
028027     END-IF.                                                      PPFEDTAX
028030     SKIP2                                                        PPFEDTAX
029743     SKIP2                                                        15611721
029744 0500-FEDERAL-TAX-GROSS.                                          PPFEDTAX
029745     SKIP1                                                        PPFEDTAX
029748***  IF      KFED-GROSS-FWT2 > ZEROS                              48861503
029749***          COMPUTE KFED-BASE = KFED-GROSS-FWT2 - KFED-AMOUNT    48861503
029750***          IF      KFED-BASE < ZEROS                            48861503
029751***                  MOVE ZEROS TO KFED-BASE                      48861503
029752***          ELSE    NEXT SENTENCE                                48861503
029753***          END-IF                                               48861503
029754***  ELSE                                                         48861503
029755***          MOVE ZEROS TO KFED-BASE                              48861503
029756***  END-IF.                                                      48861503
029757***  SKIP1                                                        48861503
029758***  MOVE ZEROS TO KFED-AMOUNT.                                   48861503
029759***  COMPUTE KFED-WRKPCT = KFED-FLAT-FWT-PERCENT / 100.           48861503
029760***  COMPUTE KFED-DED-TO-TAKE ROUNDED =  KFED-DED-TO-TAKE +       48861503
029761***                                     (KFED-BASE * KFED-WRKPCT).48861503
029762***  SKIP1                                                        48861503
029763     SKIP1                                                        48861503
029766     IF      KFED-GROSS-FWT2 > ZEROS                              48861503
029767       IF KFED-AMOUNT >= KFED-GROSS-FWT2                          48861503
029769             COMPUTE KFED-AMOUNT = KFED-AMOUNT - KFED-GROSS-FWT2  48861503
029770             MOVE ZEROS TO KFED-BASE                              48861503
029771       ELSE                                                       48861503
029773             COMPUTE KFED-BASE = KFED-GROSS-FWT2 - KFED-AMOUNT    48861503
029774             MOVE ZEROS TO KFED-AMOUNT                            48861503
029775            COMPUTE KFED-WRKPCT = KFED-FLAT-FWT-PERCENT / 100     48861503
029776*****       COMPUTE KFED-DED-TO-TAKE ROUNDED = KFED-DED-TO-TAKE + 69761691
029777*****                                   (KFED-BASE * KFED-WRKPCT) 69761691
029778            COMPUTE WS-DED-TO-TAKE  ROUNDED =                     69761691
029779              KFED-DED-TO-TAKE + (KFED-BASE * KFED-WRKPCT)        69761691
029780            MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE               69761691
029781            IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE              69761691
029782                MOVE M40090 TO KFED-MSSG-NO                       69761691
029783            END-IF                                                69761691
029784       END-IF                                                     48861503
029785     ELSE                                                         48861503
029787             MOVE ZEROS TO KFED-BASE                              48861503
029788     END-IF.                                                      48861503
029789     SKIP1                                                        48861503
029790     MOVE ZEROS TO KFED-AMOUNT.                                   48861503
029791     SKIP1                                                        48861503
029792     IF      KFED-GROSS-FWT3 > ZEROS                              48861503
029794       IF KFED-AMOUNT >= KFED-GROSS-FWT3                          48861503
029796             COMPUTE KFED-AMOUNT = KFED-AMOUNT - KFED-GROSS-FWT3  48861503
029797             MOVE ZEROS TO KFED-BASE                              48861503
029798       ELSE                                                       48861503
029800             COMPUTE KFED-BASE = KFED-GROSS-FWT3 - KFED-AMOUNT    48861503
029801             MOVE ZEROS TO KFED-AMOUNT                            48861503
029802            COMPUTE KFED-WRKPCT = KFED-FLAT-BFWT-PERCENT / 100    48861503
029803*****       COMPUTE KFED-DED-TO-TAKE ROUNDED = KFED-DED-TO-TAKE + 69761691
029804*****                                   (KFED-BASE * KFED-WRKPCT) 69761691
029805            COMPUTE WS-DED-TO-TAKE  ROUNDED =                     69761691
029806              KFED-DED-TO-TAKE + (KFED-BASE * KFED-WRKPCT)        69761691
029807            MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE               69761691
029808            IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE              69761691
029809                MOVE M40090 TO KFED-MSSG-NO                       69761691
029810            END-IF                                                69761691
029811       END-IF                                                     48861503
029812     ELSE                                                         48861503
029814             MOVE ZEROS TO KFED-BASE                              48861503
029815     END-IF.                                                      48861503
029816     SKIP1                                                        48861503
029817     MOVE ZEROS TO KFED-AMOUNT.                                   48861503
029818     SKIP1                                                        48861503
029819     IF KFED-DED-TO-TAKE <  ZERO                                  PPFEDTAX
029821       OR  KFED-FDTX-EXEMPT-EXMPT-FED-TX                          PPFEDTAX
029822       OR (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                       PPFEDTAX
029824           NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                    PPFEDTAX
029826           MOVE ZERO TO KFED-DED-TO-TAKE                          PPFEDTAX
029827     END-IF.                                                      PPFEDTAX
029828     SKIP2.                                                       PPFEDTAX
029836     GOBACK.                                                      PPFEDTAX
029837                                                                  69331612
029838*---------------------------------------------------------------  69331612
029839 1000-POSTDOC-FEL-NRA-DED  SECTION.                               69331612
029840*---------------------------------------------------------------  69331612
029841********E FEL GROSS IS ALREADY INCLUDED IN FWT1 GROSS. THUS, WE   69761691
029842********VE TO BACK IT OUT OF THE FWT1 GROSS BEFORE WE CAN DEVELOP 69761691
029843********SEPARATE POSTDOC FWT FEL DEDUCTION.                       69761691
029844**** COMPUTE WK-TOT-FWT-ADJ-GRS  =  KFED-GROSS-FWT1               69761691
029845****                             +  KFED-GROSS-FWT2               69761691
029846****                             +  KFED-GROSS-FWT3               69761691
029847****                             -  KFED-REDUCTIONS-FWT.          69761691
029848**** IF      WK-TOT-FWT-ADJ-GRS  >  KFED-POSTDOC-FEL-NRA-GRS      69761691
029849****     COMPUTE  KFED-GROSS-FWT1  =                              69761691
029850****              KFED-GROSS-FWT1  - KFED-POSTDOC-FEL-NRA-GRS     69761691
029851**** ELSE                                                         69761691
029852****     MOVE ZERO     TO KFED-GROSS-FWT1,                        69761691
029853****                      KFED-GROSS-FWT2,                        69761691
029854****                      KFED-GROSS-FWT3,                        69761691
029855****                      KFED-REDUCTIONS-FWT                     69761691
029856****     IF  WK-TOT-FWT-ADJ-GRS  >  ZERO                          69761691
029857****         MOVE WK-TOT-FWT-ADJ-GRS  TO KFED-POSTDOC-FEL-NRA-GRS 69761691
029858****     ELSE                                                     69761691
029859****         MOVE ZERO                TO KFED-POSTDOC-FEL-NRA-GRS 69761691
029860****     END-IF                                                   69761691
029861**** END-IF.                                                      69761691
029862     IF      KFED-POSTDOC-FEL-NRA-GRS  > ZERO                     69331612
029863*****   COMPUTE  KFED-FEL-FWT-DED-TO-TAKE  ROUNDED =              69761691
029864        COMPUTE  WS-DED-TO-TAKE  ROUNDED =                        69761691
029865                (KFED-POSTDOC-FEL-NRA-GRS  *                      69331612
029866                 KFED-POSTDOC-FEL-NRA-RATE)                       69331612
029867        MOVE WS-DED-TO-TAKE TO KFED-FEL-FWT-DED-TO-TAKE           69761691
029868        IF WS-DED-TO-TAKE NOT = KFED-FEL-FWT-DED-TO-TAKE          69761691
029869            MOVE M40090 TO KFED-MSSG-NO                           69761691
029870        END-IF                                                    69761691
029871     END-IF.                                                      69331612
029872     IF      KFED-FDTX-EXEMPT-EXMPT-FED-TX                        69331612
029873       OR   (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                     69331612
029874             NOT KFED-CT-CODE-NONR-ALN-SPECCTRY AND               69761691
029875             NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                  69331612
029876             MOVE ZEROS   TO KFED-FEL-FWT-DED-TO-TAKE             69331612
029880     END-IF.                                                      69331612
032200 1000-EXIT.      EXIT.                                            69331612
032300                                                                  69331612
032400 1500-NRA-DED-PROCESSING SECTION.                                 15611721
032500     SKIP1                                                        15611721
033100                                                                  15611721
033200     IF      KFED-CHECK-DATE  <  070101                           15611721
033300             GO TO 1500-EXIT                                      15611721
033400     END-IF.                                                      15611721
033500                                                                  15611721
033800     IF      KFED-GROSS-FWT1 < ZEROS                              15611721
033900     OR      KFED-GROSS-FWT1 = ZEROES                             15611721
034000             GO TO 1500-EXIT                                      15611721
034100     END-IF.                                                      15611721
034200                                                                  15611721
034300     IF      KFED-TAX-PERIOD  = 'M'                               15611721
034400             ADD KFED-MO-PARM-AMT      TO KFED-GROSS-FWT1         15611721
034600     ELSE                                                         15611721
034700        IF      KFED-TAX-PERIOD = 'S'                             15611721
034800                ADD KFED-SM-PARM-AMT   TO KFED-GROSS-FWT1         15611721
035000        ELSE                                                      15611721
035100          IF      KFED-TAX-PERIOD = 'B'                           15611721
035200                  ADD KFED-BI-PARM-AMT TO  KFED-GROSS-FWT1        15611721
035400          ELSE                                                    15611721
035500                  CONTINUE                                        15611721
035600          END-IF                                                  15611721
035700        END-IF                                                    15611721
035800     END-IF.                                                      15611721
035900                                                                  15611721
036000 1500-EXIT.                                                       15611721
036010     EXIT.                                                        15611721
036100                                                                  15611721
036200 2000-CALC-NRA-DEDUCTION.                                         28311915
036210                                                                  28311915
036220**** CALCULATE ADDITIONAL FEDERAL TAX FOR NRA                     28311915
036300     IF KFED-CT-CODE-NON-RESID-ALIEN-T OR                         28311915
036400        KFED-CT-CODE-NONR-ALN-SPECCTRY                            28311915
036500        COMPUTE S6 = XFTA-NO-NRALIEN (S4) + 23                    28311915
036600        PERFORM VARYING S5 FROM 23 BY +1                          28311915
036700          UNTIL XFTA-SALARY (S4, S5) > KFED-BASE OR (S5 = S6)     28311915
036900        END-PERFORM                                               28311915
037000        ADD -1                  TO S5                             28311915
037400        COMPUTE WS-DED-TO-TAKE ROUNDED = WS-DED-TO-TAKE +         28311915
037500                 XFTA-TAX (S4, S5)     + ((KFED-BASE    -         28311915
037700                 XFTA-SALARY (S4, S5)) * XFTA-PCT (S4, S5))       28311915
037800     END-IF.                                                      28311915
037910                                                                  28311915
038000 2000-EXIT.                                                       28311915
038100     EXIT.                                                        28311915
